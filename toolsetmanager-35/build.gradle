plugins {
	id 'eclipse'
	id 'java-library'
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

group = 'com.byrobingames'
version = rootProject.file('version.txt').text.trim()

sourceSets {
	main {
	    java {
            srcDir 'src'
        }
        resources {
        	srcDir 'src'
        	excludes = ["**/*.java"]
        }
    }
}

repositories {
    mavenCentral()
    maven {
        url "https://www.stencyl.com/dl/maven2/releases"
    }
    maven {
        url "https://www.stencyl.com/dl/maven2/snapshots"
    }
}

def stencylVersion = '4.1.0-SNAPSHOT'
dependencies {
	//Stencyl and its dependencies
    api platform("com.stencyl:stencyl-platform:$stencylVersion")
    compileOnly group: 'com.stencyl',        name: 'stencyl'
    compileOnly group: 'com.stencyl',        name: 'jide-oss'
    compileOnly group: 'com.stencyl',        name: 'jide-grids'
    compileOnly group: 'org.apache.commons', name: 'commons-lang3'
	compileOnly group: 'log4j',              name: 'log4j'
    compileOnly group: 'commons-io',         name: 'commons-io'
    compileOnly group: 'org.json',           name: 'json'
	compileOnly group: 'com.github.mizosoft.methanol', name: 'methanol'
}

jar {
	from {
		configurations.runtimeClasspath.collect { zipTree(it) }
	}
    manifest {
		attributes(
			"Extension-ID": "com.byrobingames.manager",
			"Extension-Main-Class": "com.byrobingames.manager.ByRobinGameExtension",
			"Extension-Version": version,
			"Extension-Icon": "com/byrobingames/manager/res/images/icon.png",
			"Extension-Dependencies": "engine-byRobinextensionmanager-0.2.7,stencyl-4.1.0-b11005",
			"Extension-Type": "game",
			"Extension-Name": "byRobin Extension Manager",
			"Extension-Description": "Manage byRobin Engine Extensions.",
			"Extension-Author": "Robin Schaafsma",
			"Extension-Website": "https://byrobingames.github.io",
			"Extension-Repository": "http://www.polydes.com/byrobingames",
			"Extension-Internal-Version": "8"
		)
	}
}

tasks.register('installToolsetToWorkspace', Copy) {
    from(tasks.named("jar"))
	into stencylWorkspace+"/extensions"
	rename { fileName -> "com.byrobingames.manager.jar" }
}

tasks.register('installToWorkspace') {
	dependsOn 'installToolsetToWorkspace'
}